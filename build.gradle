/*
 * This file is part of MultiLang, licensed under the MIT License.
 *
 * Copyright (c) Lorenzo0111
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */
import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "7.0.0"
    id "net.kyori.indra.publishing" version "2.0.6"
    id "io.github.slimjar" version "1.2.1"
    id "org.sonarqube" version "3.3"
}

ext {
    libsBase = 'me.lorenzo0111.multilang.libs'
}

apply from: "https://cdn.rocketplugins.space/spigot"

group = "me.lorenzo0111"
version = "1.4"

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://repo.codemc.org/repository/maven-snapshots/' }
    maven { url 'https://repo.repsy.io/mvn/lorenzo0111/public' }
    maven { url 'https://repo.mattstudios.me/artifactory/public/' }
    maven { url 'https://oss.sonatype.org/content/repositories/releases/' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/'}
    maven { url "https://repo.dmulloy2.net/repository/public/" }
    maven { url "https://repo.vshnv.tech/releases/" }
}

dependencies {
    implementation("space.rocketplugins.pluginslib:bukkit:2.0.2.1")
    implementation("io.github.slimjar:slimjar:1.2.5")
    implementation("dev.triumphteam:triumph-gui:3.0.3")
    implementation("com.zaxxer:HikariCP:5.0.0")
    compileOnly("com.comphenix.protocol:ProtocolLib:4.7.0")
    compileOnly("com.github.MilkBowl:VaultAPI:1.7.1")
    compileOnly("me.lorenzo0111:RocketPlaceholders:1.9.2")
    slim("com.github.cryptomorin:XSeries:8.3.0")
    slim("net.kyori:adventure-api:4.8.1")
    slim("net.kyori:adventure-text-serializer-legacy:4.8.1")
    slim("mysql:mysql-connector-java:8.0.26")
    slim("org.xerial:sqlite-jdbc:3.36.0.1")
}

sonarqube {
    properties {
        property("sonar.projectKey", "Lorenzo0111_MultiLang")
    }
}

slimJar {
    relocate('dev.triumphteam',"${libsBase}")
    relocate('google',"${libsBase}.google")
    relocate('org.slf4j',"${libsBase}.slf4j")
    relocate('org.sqlite.native',"${libsBase}.jdbc.sqlite")
    relocate('com.cryptomorin.xseries',"${libsBase}.xseries")
    relocate('com.zaxxer.hikari',"${libsBase}.hikari")
}

shadowJar {
    minimize()
    archiveFileName.set("${project.name}-${project.version}.jar")
    relocate("io.github.slimjar","${libsBase}.slimjar")
    relocate("dev.triumphteam","${libsBase}")
    relocate("google","${libsBase}.google")
    relocate("org.slf4j","${libsBase}.slf4j")
    relocate("org.sqlite.native","${libsBase}.jdbc.sqlite")
    relocate("com.cryptomorin.xseries","${libsBase}.xseries")
    relocate("com.zaxxer.hikari","${libsBase}.hikari")
}


javadoc {
    options.links(
            'https://javadoc.io/static/org.jetbrains/annotations/20.1.0/',
            'https://docs.oracle.com/javase/8/docs/api/',
            'https://papermc.io/javadocs/paper/1.16/'
    )
    source = sourceSets.main.allJava
    include("**/api/*")
    destinationDir = new File("build/javadocs")
}

indra {
    github("Lorenzo0111", "MultiLang")
    mitLicense()
    publishReleasesTo("codeMc", "https://repo.codemc.io/repository/maven-releases/")
    publishSnapshotsTo("codeMc", "https://repo.codemc.io/repository/maven-snapshots/")
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        duplicatesStrategy DuplicatesStrategy.INCLUDE
        filter ReplaceTokens, tokens: [version: version]
    }
}

tasks.build.dependsOn tasks.shadowJar
