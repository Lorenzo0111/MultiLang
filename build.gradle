/*
 * This file is part of MultiLang, licensed under the MIT License.
 *
 * Copyright (c) Lorenzo0111
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */
import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "net.kyori.indra.publishing" version "2.1.0"
    id "net.kyori.indra.publishing.sonatype" version "2.1.1"
    id "signing"
    id "io.github.slimjar" version "1.3.0"
    id "org.sonarqube" version "3.3"
}

ext {
    libsBase = 'me.lorenzo0111.multilang.libs'
}

group = "me.lorenzo0111"
version = "1.7"
description = "MultiLang is a library that allows you to easily create multi-language placeholders."

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
    maven { url "https://jitpack.io" }
    maven { url "https://repo.codemc.org/repository/maven-public" }
    maven { url "https://repo.codemc.org/repository/maven-snapshots/" }
    maven { url "https://repo.codemc.org/repository/maven-releases/" }
    maven { url "https://repo.repsy.io/mvn/lorenzo0111/public" }
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/"}
    maven { url "https://repo.dmulloy2.net/repository/public/" }
    maven { url "https://dl.lorenzo0111.me/releases" }
}

dependencies {
    implementation('org.bstats:bstats-bukkit:3.0.0')
    implementation("space.rocketplugins.pluginslib:bukkit:2.0.3.2")
    implementation("io.github.slimjar:slimjar:1.2.7")
    implementation('dev.triumphteam:triumph-gui:3.1.1')
    implementation files("libs/GoogleTranslateAPI-1.0-shaded.jar")
    compileOnly("org.spigotmc:spigot-api:1.18-R0.1-SNAPSHOT")
    compileOnly("com.comphenix.protocol:ProtocolLib:4.7.0")
    compileOnly("com.github.MilkBowl:VaultAPI:1.7.1")
    compileOnly("me.clip:placeholderapi:2.11.1")
    compileOnly("me.lorenzo0111:RocketPlaceholders:2.1")
    compileOnly('org.jetbrains:annotations:23.0.0')
    slim("com.zaxxer:HikariCP:4.0.3")
    slim('com.github.cryptomorin:XSeries:8.6.1')
    slim("mysql:mysql-connector-java:8.0.28")
    slim("org.xerial:sqlite-jdbc:3.36.0.3")
    slim("org.slf4j:slf4j-simple:1.7.35")
}

sonarqube {
    properties {
        property("sonar.projectKey", "Lorenzo0111_MultiLang")
    }
}

slimJar {
    relocate('dev.triumphteam',"${libsBase}")
    relocate('com.cryptomorin.xseries',"${libsBase}.xseries")
    relocate('com.zaxxer.hikari',"${libsBase}.hikari")
}

shadowJar {
    minimize()
    archiveFileName.set("${project.name}-${project.version}.jar")
    relocate("io.github.slimjar","${libsBase}.slimjar")
    relocate("dev.triumphteam","${libsBase}")
    relocate("com.cryptomorin.xseries","${libsBase}.xseries")
    relocate("net.kyori.adventure", "${libsBase}.adventure")
    relocate("net.kyori.examination", "${libsBase}.examination")
    relocate("com.google.j2objc", "${libsBase}.google.j2objc")
    relocate("com.google.common", "${libsBase}.google.common")
    relocate("com.google.errorprone", "${libsBase}.google.errorprone")
    relocate("org.yaml.snakeyaml", "${libsBase}.yaml")
    relocate("org.spongepowered.configurate", "${libsBase}.configurate")
    relocate("org.bstats", "${libsBase}.bstats")
    relocate("com.google.gson", "${libsBase}.google.gson")
    relocate("com.gtranslate", "${libsBase}.google.translate")
}


javadoc {
    options.links(
            'https://javadoc.io/static/org.jetbrains/annotations/20.1.0/',
            'https://docs.oracle.com/javase/8/docs/api/',
            'https://papermc.io/javadocs/paper/1.16/'
    )
    source = sourceSets.main.allJava
    include("**/api/*")
    destinationDir = new File("build/javadocs")
}

indra {
    github("Lorenzo0111", "MultiLang")
    mitLicense()
    publishReleasesTo("codeMc", "https://repo.codemc.io/repository/maven-releases/")
    publishSnapshotsTo("codeMc", "https://repo.codemc.io/repository/maven-snapshots/")

    configurePublications {
        pom {
            developers {
                developer {
                    id.set("lorenzo0111")
                }
            }
        }
    }
}

indraSonatype {
    useAlternateSonatypeOSSHost("s01")
}

signing {
    sign configurations.archives
    useGpgCmd()
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        duplicatesStrategy DuplicatesStrategy.INCLUDE
        filter ReplaceTokens, tokens: [version: version]
    }
}

java.sourceCompatibility = JavaVersion.VERSION_1_8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.build.dependsOn tasks.shadowJar
